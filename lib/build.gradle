description = "Sub project regrouping each 'unitary' jar."

subprojects {

    apply plugin: "java-library"
    apply plugin: "idea"
    apply plugin: "maven-publish"
    apply plugin: 'signing'


    publishing {

        repositories {
            maven {
                name = 'NexusDocker'

                def releasesRepoUrl = "http://localhost:8081/nexus/content/repositories/releases/"
                def snapshotsRepoUrl = "http://localhost:8081/nexus/content/repositories/snapshots/"

                def repoUrl = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                url = repoUrl

                credentials {
                    username "admin"
                    password "admin123"
                }
            }
        }

        repositories {
            maven {
                name = 'LocalRepo'
                url = "file://${rootDir}/repo"
            }
        }
    }

    signing {
        // No need to configure signing if in snapshot
        required { !version.endsWith('SNAPSHOT') }
    }

    dependencies{
        testImplementation "junit:junit:4+"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

}

//task publishToLocalRepository {
//    group = 'publishing'
//    description = 'Publishes all Maven publications to the internal Maven repository.'
//    dependsOn tasks.withType(PublishToMavenLocal)
//    dependsOn subprojects.collect { it.tasks.withType(PublishToMavenLocal) }
//}

//task publishToTestRepository {
//    group = 'publishing'
//    description = 'Publishes all Maven publications to the test (project root) Maven repository.'
//    dependsOn tasks.withType(PublishToMavenRepository)
//    dependsOn subprojects.collect {it.tasks.withType(PublishToMavenRepository) }
//}

/**
 * This methods helps modifying the pom for each submodule.
 * The common part is defined here. Only the parameters are to be provided by the submodule.
 * @param nameStr the display name of the submodule
 * @param descriptionStr the description of the submodule
 * @return
 */
Action<? super MavenPom> createConfigureActionForPom (String nameStr, String descriptionStr) {
    return new Action() {
        void execute(Object pom) {
            pom.licenses {
                license {
                    name = 'GNU LESSER GENERAL PUBLIC LICENSE, Version 3.0'
                    url = 'https://www.gnu.org/licenses/lgpl-3.0.txt'
                }
            }

            pom.developers {
                developer {
                    id = 'acasteigts'
                    name = 'Arnaud Casteigts'
                    email = 'acasteigts@labri.fr'
                }
            }
            pom.scm {
                connection = 'scm:git:git://github.com/acasteigts/jbotsim.git'
                developerConnection = 'scm:git:ssh://github.com/acasteigts/jbotsim.git'
                url = 'https://github.com/acasteigts/jbotsim/tree/master'
            }

            pom.name = nameStr
            pom.description = descriptionStr
            pom.url = 'https://jbotsim.io/'
        }
    }
}